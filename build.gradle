plugins {
    id 'maven'
    id 'java'
    id 'idea'
    id 'com.adarshr.test-logger' version '1.7.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    mavenCentral()
    maven {
        // web3j snapshots
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        // Aion dependencies
        url "$rootProject.projectDir/lib"
    }
}

dependencies {
    implementation "org.aion:avm-api:1.4"
    implementation "org.aion:avm-tooling:1.4"
    implementation "org.aion:avm-userlib:1.4"
    
    implementation "org.web3j:core:4.4.0-SNAPSHOT"
    implementation "org.web3j:web3j-aion-avm:0.1.0-SNAPSHOT"
    implementation "org.web3j:web3j-aion-codegen:0.1.0-SNAPSHOT"
    
    implementation "ch.qos.logback:logback-classic:1.2.3"
    
    runtimeOnly "com.github.schnitker.logmanager:logmgr-logback:1.0.2"

    testImplementation "junit:junit:4.12"
}

testlogger {
    showStandardStreams = true
}

sourceSets {
    main {
        java {
            srcDir {
                "$project.buildDir/generated/source/web3j-aion/main/java"
            }
        }
    }
}

task generateContractWrappers(type: JavaExec, group: 'aion', dependsOn: 'clean') {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.web3j.aion.codegen.AionGeneratorMain'
    
    args '--abiFile', "$projectDir/hello-world-avm/target/hello-world-avm-${version}.abi",
            '--binFile', "$projectDir/hello-world-avm/target/hello-world-avm-${version}.jar",
            '--outputDir', "$project.buildDir/generated/source/web3j-aion/main/java",
            '--package', 'org.web3j.aion.samples.generated'
}

compileJava {
    source "$project.buildDir/generated/source/web3j-aion/main/java"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    source "$project.buildDir/generated/source/web3j-aion/main/java"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    dependsOn 'generateContractWrappers'
}

