plugins {
    id 'maven'
    id 'java'
    id 'idea'
}

group 'org.web3j.aion'
version '0.1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven {
        // web3j snapshots
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        // Aion dependencies
        url "$rootProject.projectDir/lib"
    }
    maven {
        // web3j imported dependencies
        url 'https://dl.bintray.com/ethereum/maven/'
    }
}

dependencies {
    implementation "org.web3j:core:4.4.0-SNAPSHOT"
    implementation "org.web3j:web3j-aion-avm:0.1.0-SNAPSHOT"
    implementation "org.web3j:web3j-aion-codegen:0.1.0-SNAPSHOT"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.2"
}

tasks.withType(Test) {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDir {
                "$project.buildDir/generated/source/web3j-aion/main/java"
            }
        }
    }
}

task generateContractWrappers(type: JavaExec, group: 'aion', dependsOn: 'clean') {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.web3j.aion.codegen.AionGeneratorMain'
    
    args '--abiFile', "$projectDir/HelloAvm.abi",
            '--binFile', "$projectDir/HelloAvm.jar",
            '--outputDir', "$project.buildDir/generated/source/web3j-aion/main/java",
            '--package', 'org.web3j.aion.samples.generated',
            '--targetVm', 'AVM'
}

compileJava {
    source "$project.buildDir/generated/source/web3j-aion/main/java"
}

compileTestJava {
    source "$project.buildDir/generated/source/web3j-aion/main/java"
    dependsOn 'generateContractWrappers'
}

